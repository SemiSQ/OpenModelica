### PREPROCESSOR and FLAGS
QMAKE_LFLAGS += -enable-auto-import

DEFINES += __x86__ \
    __NT__ \
    __OSVERSION__=4 \
    __WIN32__ \
    WIN32 \
    _WINDOWS \
    _USRDLL \
    _CRT_SECURE_NO_DEPRECATE \
    _WIN32_WINNT=0x0400 \
    _MBCS \
    QWT_DLL

### FOLDERS, INCLUDES
DESTDIR = ../bin
DEPENDPATH += ../bin


CONFIG(debug, debug|release){
    UI_DIR = debug/generatedfiles/ui
    MOC_DIR = debug/generatedfiles/moc
    RCC_DIR = debug/generatedfiles/rcc

    INCLUDEPATH  += ../OMOptimBasis/build/debug/generatedfiles/ui
    INCLUDEPATH  += ../build/debug/generatedfiles/ui

}else {
    UI_DIR = release/generatedfiles/ui
    MOC_DIR = release/generatedfiles/moc
    RCC_DIR = release/generatedfiles/rcc

    INCLUDEPATH  += ../OMOptimBasis/build/release/generatedfiles/ui
    INCLUDEPATH  += ../build/release/generatedfiles/ui
}

INCLUDEPATH += $$(OMDEV)/lib/omniORB-4.1.6-mingw/include \
               $$(OMDEV)/lib/qwt-5.2.1-mingw/include \
               $$(OMDEV)/lib/paradiseo-mingw/paradiseo-eo/src \
               $$(OMDEV)/lib/paradiseo-mingw/paradiseo-eo/src/utils \
               $$(OMDEV)/lib/paradiseo-mingw/paradiseo-moeo/src \
               $$(OMDEV)/lib/paradiseo-mingw/paradiseo-mo/src

### LIBRARIES (order has an impact !!)
CONFIG(debug, debug|release){
    LIBS += -L$$(OMDEV)/lib/omniORB-4.1.6-mingw/bin/x86_win32 \
    -lomniORB416_rtd \
    -lomnithread34_rtd \
    -lomniDynamic416_rtd \
    -L$$(OMDEV)/lib/qwt-5.2.1-mingw/lib \
    -lqwtd5 \
    -L../bin \
    -lOMOptimBasisd

    PRE_TARGETDEPS += ../bin/libOMOptimBasisd.a

}else {
    LIBS += -L$$(OMDEV)/lib/omniORB-4.1.6-mingw/bin/x86_win32 \
    -lomniORB416_rt \
    -lomnithread34_rt \
    -lomniDynamic416_rt \
    -L$$(OMDEV)/lib/qwt-5.2.1-mingw/lib \
    -lqwt5 \
    -L../bin \
    -lOMOptimBasis

    PRE_TARGETDEPS += ../bin/libOMOptimBasis.a
}

LIBS += \
    -L$$(OMDEV)/lib/paradiseo-mingw/paradiseo-eo/lib \
    -leo \
    -leoutils \
    -L$$(OMDEV)/lib/paradiseo-mingw/paradiseo-moeo/lib \
    -lmoeo





